#!/usr/bin/env spang2
# @option -vac --time
# @endpoint togosite
# @param annotation_type = Helix

PREFIX up: <http://purl.uniprot.org/core/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>

SELECT DISTINCT ?uniprot ?mnemonic ?seq_length ?region_length ?begin_position ?end_position
FROM <http://rdf.integbio.jp/dataset/togosite/uniprot>
WHERE {
  ?uniprot up:proteome ?subset ;
      up:mnemonic ?mnemonic;
      up:annotation ?annotation .
  ?annotation a up:${annotation_type}_Annotation ;
      up:range ?range .
  ?range faldo:begin/faldo:position ?begin_position ;
         faldo:end/faldo:position ?end_position .
  ?range faldo:begin/faldo:reference/rdf:value ?seq .
  BIND(STRLEN(?seq) AS ?seq_length)
  BIND((?end_position - ?begin_position + 1) AS ?region_length)
  FILTER(REGEX(STR(?subset), "UP000005640"))
}
ORDER BY ?uniprot
