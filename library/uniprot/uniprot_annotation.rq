#!/usr/bin/env spang2
# @endpoint uniprot
# @title Get annotations of a UniProt entry
# @param uniprot=K9Z723
# @option -vac

SELECT ?annot_type ?begin_pos ?end_pos ?property ?val ?conflict_seq
WHERE {
    uniprot:${uniprot} up:annotation ?annot .
    ?annot a ?type ;
           ?property ?val .
    ?type rdfs:label ?annot_type .
    OPTIONAL {
        ?annot up:range ?range .
        ?range faldo:begin/faldo:position ?begin_pos ;
               faldo:end/faldo:position ?end_pos .
    }
    OPTIONAL {
        ?annot up:conflictingSequence ?conflict_seq .
    }
    FILTER (?property != rdf:type && ?property != up:range && ?property != up:conflictingSequence)
}
ORDER BY ?annot_type ?begin_pos

